# Copyright (C) 2011 Codership Oy <info@codership.com>

env = DefaultEnvironment() #.Clone()

conf = Configure(env)

if not conf.CheckLibWithHeader(libs=['boost_program_options'],
                               header='boost/program_options.hpp',
                               language='CXX',
                               autoadd=0):
    print 'Error: boost_program_options not found'
    Exit (1)

env.Prepend(LIBS=File('#/galerautils/src/libgalerautils.a'))
env.Prepend(LIBS=File('#/galerautils/src/libgalerautils++.a'))
env.Prepend(LIBS=File('#/gcomm/src/libgcomm.a'))
env.Prepend(LIBS=File('#/gcs/src/libgcs4garb.a'))

bpo_static = '/usr/lib/libboost_program_options.a'
bpo_local_static = '/usr/local/lib/libboost_program_options.a'

import os

if os.path.isfile(bpo_static):
    env.Append(LIBS=File(bpo_static))
elif os.path.isfile(bpo_local_static):
    env.Append(LIBS=File(bpo_local_static))
else:
    env.Append(LIBS=['boost_program_options'])

# special environment for garb_config.cpp
conf_env = env.Clone()
Import('GALERA_VER', 'GALERA_REV')
conf_env.Append(CPPFLAGS = ' -DGALERA_VER=\\"' + GALERA_VER + '\\"')
conf_env.Append(CPPFLAGS = ' -DGALERA_REV=\\"' + GALERA_REV + '\\"')

garb = env.Program(target = 'garbd',
                   source = Split('''
                                  garb_logger.cpp
                                  garb_gcs.cpp
                                  garb_recv_loop.cpp
                                  garb_main.cpp
                              ''')
                              +
                              conf_env.SharedObject(['garb_config.cpp'])
                   )

