
env = DefaultEnvironment()

libgaleraxx_env = env.Clone()

libgaleraxx_env.Replace(CXXFLAGS = env['CXXFLAGS'].replace('-pedantic',''))
libgaleraxx_env.Append(CXXFLAGS = ' -DGALERA_LOG_H_ENABLE_CXX')


libgaleraxx_srcs = [
    'mapped_buffer.cpp',
    'write_set.cpp',
    'trx_handle.cpp',
    'wsdb.cpp',
    'certification.cpp',
    'galera_service_thd.cpp',
    'wsrep_params.cpp',
    'gcs_action_source.cpp',
    'galera_info.c']

libgaleraxx_env.StaticLibrary('galera++', libgaleraxx_srcs)

# Environment for multimaster library build
mmenv = libgaleraxx_env.Clone()
mmenv.Append(CPPFLAGS = ' -DGALERA_MULTIMASTER')
mmenv.Replace(SHOBJPREFIX = 'libmmgalera++-')


#
env.Append(LIBGALERA_OBJS = libgaleraxx_env.SharedObject(libgaleraxx_srcs))
env.Append(LIBMMGALERA_OBJS = mmenv.SharedObject([
            'replicator_smm.cpp',
            'replicator_smm_stats.cpp',
            'replicator_smm_params.cpp',
            'wsrep_provider.cpp'
            ]))
